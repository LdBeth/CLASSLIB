 C←A ZGCD B;⎕IO;RHO;M;U;V;I;Q;T
⍝ C  IS RETURNED AS THE ENTRY-BY-ENTRY GCD OF THE INTEGER
⍝ ARRAYS  A  AND  B.
⍝ THE VARIABLES r AND s EXPRESS C AS(r×A)+s×B.
 ⎕IO←1
 →NOTEST/BEGIN
 DERR∧/(,A=⌊A),,B=⌊B
⍝ TEST FOR CONFORMABILITY.
 EXPAND
⍝ REPLACE  A  AND  B  BY THEIR RAVELS AND
⍝ APPLY THE EUCLIDEAN ALGORITHM.
BEGIN:M←×/RHO←⍴A
 U←(3,M)⍴(×A),(M⍴0),|A←,A
 V←(⍴U)⍴(M⍴0),(×B),|B←,B
 I←⍳M
LOOP:→(0=⍴I←(V[3;I]≠0)/I)/END
 T←U[;I]-V[;I]×(3,⍴I)⍴⌊U[3;I]÷V[3;I]
 U[;I]←V[;I]
 V[;I]←T
 →LOOP
END:C←RHO⍴U[3;]
 r←RHO⍴U[1;]
 s←RHO⍴U[2;]
