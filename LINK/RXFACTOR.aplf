 G←RXFACTOR F;⎕IO;D;TOL;N;GCD;DF;r;s;H;S;ADDH;CENT;DEL;DER;DH;DR;MAX;NEWR;a;A;CLOSE;CNT;I;ND;SQ;S2;U;VAL;X
⍝ ATTEMPTS TO PRODUCE A LIST OF MONIC REAL
⍝ IRREDUCIBLE POLYNOMIALS WHOSE PRODUCT IS THE
⍝ MONIC ASSOCIATE OF A GIVEN POLYNOMIAL.
 ⎕IO←0
 DERR∧/(1=⍴⍴F),,0<D←RXDEGREE F
 TOL←EPSILON×⌈/,|F←((D+1)↑F)÷F[D]
 SQ←4 2⍴1 1 ¯1 1 ¯1 ¯1 1 ¯1
 N←+/∧\0=F
 G←(N,3)⍴0 1 0
AGAIN:GCD←F RXGCD DF←1↓F×⍳⍴F←N↓F
 DERR∧/TOL≥|GCD RXDIFF(F RXPROD r)RXSUM DF RXPROD s
 →(2<D←⍴H←F RXQUOT GCD)/NONLIN
 CENT←1 2⍴2↑-H[0]÷H[1]
 →CLEANUP
NONLIN:CENT←(2 2↑SQ)×S←0.5×⌈/(⌽D×|¯1↓H)*÷1↓⍳D
 ADDH←|1↓DH×⍳⍴DH←1↓H×⍳⍴H
 DH←DH,0
 CLOSE←CNT←0
LOOP:→(15<CNT←CNT+1)/CLEANUP
 DER←VAL←(⍴CENT)⍴0
 I←D
EVAL:→(0>I←I-1)/DONE
 VAL←((⍴VAL)⍴H[I],0)+(-/CENT×VAL),[0.5]+/CENT×⌽VAL
 DER←((⍴DER)⍴DH[I],0)+(-/CENT×DER),[0.5]+/CENT×⌽DER
 →EVAL
DONE:X←~TOL≥DR←(ND←+/DER×DER)*0.5
 DEL←(X⌿DER×(⍴DER)⍴1 ¯1)÷ND,[0.5]ND←X/ND
 DEL←(-/VAL×DEL),[0.5]+/DEL×⌽VAL←X⌿VAL
 →CLOSE/NEWTON
 MAX←(S2←S×1.415)+(+/X⌿CENT×CENT)*0.5
 MAX←(MAX∘.*⍳⍴ADDH)+.×ADDH
 NEWR←MAX×S×S÷X/DR
 X←(~X)∨X\(NEWR+S2)≥(+/DEL×DEL)*0.5
 CENT←(4 1×⍴CENT)⍴0 2 1 2⍉(CENT←X⌿CENT)∘.+SQ×S←S÷2
 CLOSE←(8≤CNT)∨(1↑⍴CENT)≤1↑⍴DER
 →LOOP
NEWTON:CENT←(X⌿CENT)-DEL
 →(∧/,TOL≥|VAL)/CLEANUP
 →LOOP
CLEANUP:CENT←(CENT[;1]≥0)⌿CENT
 D←⍴F
 I←¯1
NEXT:→((1↑⍴CENT)≤I←I+1)/END
 A←CENT[I;]
 →(A[1]≠0)/COMPLEX
 U←(-A[0]),1
 →CHECK
COMPLEX:U←(+/A×A),(-2×A[0]),1
CHECK:→(~∧/TOL≥|U RXREM F)/NEXT
 G←G,[0]3↑U
 F←F RXQUOT U
 →CHECK
END:DERR D>⍴F
 →(1<⍴F)/AGAIN
 G←G×~(|G)≤EPSILON×⌈/,|G
