 C←GAUSSFACTOR A;⎕IO;P;I;J;U;Q;NOTEST
⍝ PRODUCES A LIST OF GAUSSIAN PRIMES WHOSE PRODUCT
⍝ IS AN ASSOCIATE OF THE NONZERO GAUSSIAN INTEGER  A.
⍝ NO RATIONAL PRIME IN THE NORM OF  A  MAY BE
⍝ LARGER THAN 10000.
 DERR∧/(,A=⌊A),(1≥⍴⍴A),(∨/,A≠0),(×/⍴A)∊1 2
 C←0 2⍴0
 ⎕IO←1
 →(0=⍴P←SSORT ZFACTOR A+.×A←2↑A)/0
 DERR 10000>¯1↑P
 Q←((2=P[1]),2)⍴1 1
 Q←Q,[1]((3=4|P)/P),[1.5]0
 NOTEST←1
 →(0=⍴P←(1=4|P)/P)/LOOP
 I←SFEL U=⌊U←(0⌈P∘.-(⍳⌊(¯1↑P)*0.5)*2)*0.5
 Q←Q,[1]⍉(I,I),[0.5]J,-J←⌊(P-I×I)*0.5
LOOP:→(0=⍴Q←(∧/0=Q GAUSSREM(⍴Q)⍴A)⌿Q)/END
 C←C,[1]Q
 A←⌊A CQUOT CPRODRED Q
 →LOOP
END:C←C[⍋C[;2];]
 C←C[⍋+/C*2;]
