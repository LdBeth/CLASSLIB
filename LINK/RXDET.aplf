 D←RXDET A;⎕IO;V;M;X;J;W;Q;R;S;U;DEG;a;NOTEST
⍝ COMPUTES THE DETERMINANT OF A MATRIX
⍝ OF REAL POLYNOMIALS.
 DERR(3=⍴⍴A)∧=/2↑⍴A
 D←,⎕IO←1
 →(0=1↑⍴A)/0
 A←A×(|A)≥EPSILON×⌈/,|A
 NOTEST←1
LOOP:→(1=1↑⍴A)/END
BACK:→(∧/¯1=DEG←RXDEGREE V←A[;1;])/ZERO
 M←⌊/(DEG>¯1)/DEG
 X←(M=DEG)/⍳⍴DEG
 J←X[(|V[X;M+1])⍳⌈/|V[X;M+1]]
 →(J=1)/OK
 A[1,J;;]←A[J,1;;]
 D←-D
OK:→(∧/,0=W←1 0↓A[;1;])/ENDLP
 Q←(-1 0+⍴U)↑U←W RXQUOT A[1;1;]
 R←TRAV((2↑⍴A),¯1↑⍴Q)⍴Q
 S←(⍴A)⍴A[1;;]
 A←A RXDIFF R RXPROD S
 →BACK
ENDLP:D←D RXPROD A[1;1;]
 A←1 1 0↓A
 →LOOP
ZERO:D←,0
 →0
END:D←D RXPROD A[1;1;]
