∇B←QNRMLZ A;D;RHO
  ⍝ COMPUTES THE STANDARD REPRESENTATION OF AN ARRAY OF
  ⍝ RATIONAL NUMBERS EXPRESSED AS QUOTIENTS OF INTEGERS.
  ⍝ FOR SCALARS AND ARRAYS OF VECTORS OF LENGTH 1 A
  ⍝ DENOMINATOR OF 1 IS ADDED.
  →NOTEST/BEGIN
  DERR QTEST A
BEGIN:→((0=⍴⍴B)∨1=¯1↑⍴B←A)/ADDDEN
  RHO←¯1↓⍴A
  D←(((RHO,1)↑A)ZGCD0 D)××D←(RHO,¯1)↑A
  B←⌊A÷D,D
  →0
ADDDEN:B←A,1
  ∇

∇C←A QDIFF B
  ⍝ COMPUTES THE DIFFERENCE OF TWO RATIONAL ARRAYS.
  C←A QSUM QNEG B
∇

∇C←A QSUM B;RA;RB;AN;AD;BN;BD;NT
  ⍝ COMPUTES THE SUM OF TWO RATIONAL ARRAYS.
  →NOTEST/BEGIN
  A←QNRMLZ A
  B←QNRMLZ B
  EXPANDV
BEGIN:RA←¯1↓⍴A
  RB←¯1↓⍴B
  AN←(RA,1)↑A
  AD←(RA,¯1)↑A
  BN←(RB,1)↑B
  BD←(RB,¯1)↑B
  NT←NOTEST
  NOTEST←1
  C←QNRMLZ((AN×BD)+AD×BN),AD×BD
  NOTEST←NT
∇

∇B←QNEG A
  ⍝ COMPUTES THE NEGATIVE OF A RATIONAL ARRAY.
  →NOTEST/BEGIN
  A←QNRMLZ A
BEGIN:B←A×(⍴A)⍴¯1 1
∇

∇B←QINV A;D
  ⍝ COMPUTES THE RECIPROCAL OF A RATIONAL ARRAY.
  →NOTEST/BEGIN
  A←QNRMLZ A
  DERR∧/0≠,1 0/A
BEGIN:B←(⌽A)×D,D←×1 0/A
∇

∇C←A QPOWER B;RHO;RC;I;J;NOTEST
  ⍝ COMPUTES THE B-TH POWER OF THE RATIONAL
  ⍝ ARRAY  A  USING THE BINARY POWER ALGORITHM.
  NOTEST←0
  A←QNRMLZ A
  DERR∧/(,B=⌊B),,B≥0
  B←((⍴B),1)⍴B
  EXPANDV
  RC←(×/¯1↓RHO←⍴A),2
  A←RC⍴A
  C←RC⍴1 1
  I←(B>0)/⍳⍴B←,B
  NOTEST←1
LOOP:C[J;]←C[J;]QPROD A[J←(2|B[I])/I;]
  →(0=⍴I←(B[I]≥2)/I)/END
  A[I;]←A[I;]QPROD A[I;]
  B[I]←⌊B[I]÷2
  →LOOP
END:C←RHO⍴C
∇

∇C←A QMATPROD B;⎕IO;X;AX;BX;RR;NOTEST;RA;RB
  ⍝ COMPUTES THE MATRIX PRODUCT OF TWO NONSCALAR ARRAYS
  ⍝ OF RATIONAL NUMBERS.
  ⎕IO←1
  NOTEST←0
  A←QNRMLZ A
  B←QNRMLZ B
  DERR∧/2≤(⍴⍴A),⍴⍴B
  DERR(⍴A)[¯1+⍴⍴A]=1↑⍴B
  C←((RA←¯2↓⍴A),(RB←¯1↓1↓⍴B),2)⍴0 1
  X←1=⍳1↑⍴B
  RR←((⍴RA)+⍳⍴RB),(⍳⍴RA),⍴⍴C
  NOTEST←1
LOOP:AX←RR⍉(RB,RA,2)⍴X/[¯1+⍴⍴A]A
  BX←(⍴C)⍴X⌿B
  C←C QSUM AX QPROD BX
  →(~1↑X←¯1⌽X)/LOOP
∇

∇C←A QPROD B;NT
  ⍝ COMPUTES THE PRODUCT OF TWO RATIONAL ARRAYS.
  →NOTEST/BEGIN
  A←QNRMLZ A
  B←QNRMLZ B
  EXPANDV
BEGIN:NT←NOTEST
  NOTEST←1
  C←QNRMLZ A×B
  NOTEST←NT
∇

∇C←A QQUOT B
  ⍝ COMPUTES THE QUOTIENT OF TWO RATIONAL ARRAYS.
  C←A QPROD QINV B
∇

∇T←QTEST A
  ⍝ CHECKS WHETHER  A  REPRESENTS AN ARRAY
  ⍝ OF RATIONAL NUMBERS.
  →(~T←(∧/,A=⌊A)∧(0=⍴⍴A)∨(0<⍴⍴A)∧∨/1 2=¯1↑⍴A)/0
  →((0=⍴⍴A)∨1=¯1↑⍴A)/0
  T←∧/0≠,0 1/A
∇
