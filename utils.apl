∇DERR T
  ⍝ IF  T  IS FALSE, A MESSAGE IS PRINTED AND ALL ACTIVE
  ⍝ PROCEDURES ARE TERMINATED.
  →T/0
  'PROCEDURE DOMAIN ERROR'
  →
∇
∇X←DAQ A;NA;RA;M;K;R
  ⍝ PRODUCES THE CHARACTER ARRAY FOR DISPLAYING AN
  ⍝ ARRAY OF RATIONAL NUMBERS.
  X←''
  →(0=NA←×/⍴A)/0
  RA←⍴A←QNRMLZ A
  M←1⌈¯1↓¯2↑RA
  K←¯1↑⍴X←⍕(NA,1)⍴A
  R←(¯1++/X=' ')×NA⍴0 1
  X←R⌽X
  X←((×/¯1↓RA),2×K)⍴X
  X[;K+⎕IO]←'/'
  X←X,' '
  X←((¯2↓RA),M×^1↑⍴X)⍴X
∇

∇X←P DARV A;NA;RA;M;K
  ⍝ PRODUCES THE CHARACTER ARRAY DISPLAYING AN ARRAY
  ⍝ OF REAL VECTORS WITH P DECIMAL PLACES.
  X←''
  →(0=NA←×/RA←⍴A)/0
  M←1⌈¯1↓¯2↑RA
  K←¯1↑⍴X←P⍕(NA,1)⍴A
  X←((×/¯1↓RA),K×¯1↑RA)⍴X
  X←(0 2+⍴X)↑X
  X←((¯2↓RA),M×¯1↑⍴X)⍴X
∇

∇X←DAZV A
  ⍝ PRODUCES THE CHARACTER ARRAY DISPLAYING AN ARRAY
  ⍝ OF INTEGER VECTORS OR REAL VECTORS ROUNDED TO THE
  ⍝ NEAREST INTEGER.
  X←''
  →(0=×/⍴A)/0
  X←0 DARV A
∇

∇EXPANDV;RA;NA;RB;NB
  ⍝ TESTS IF TWO ARRAYS OF VECTORS ARE CONFORMABLE FOR
  ⍝ SCALAR OPERATIONS AND IF SO, EXPANDS ONE, IF
  ⍝ NECESSARY, SO THAT THEY HAVE THE SAME SHAPE ALONG ALL
  ⍝ BUT THE LAST AXIS.  IF THEY ARE NOT CONFORMABLE, ALL
  ⍝ PROCESSING IS STOPPED.
  ⍝ SCALARS ARE REPLACED BY VECTORS OF LENGTH 1.
  →(1≤⍴⍴A)/CHECKB
  A←,A
CHECKB:→(1≤⍴⍴B)/NEXT
  B←,B
  ⍝ IF  A  OR  B  HAS ONE ENTRY, THE ARE CONFORMABLE.
NEXT:→(∧/1=(NA←×/RA←¯1↓⍴A),NB←×/RB←¯1↓⍴B)/EXP
  →((⍴RA)≠⍴RB)/RNKERR
  →(∧/RA≠RB)/LENERR
  →0
RNKERR:'VECTOR RANK ERROR'
  →
LENERR:'VECTOR LENGTH ERROR'
  →
EXP:→((NA≠1)∧(NA=1)∧(NB=1)∧(⍴RA)>⍴RB)/EXPB
  A←(RB,¯1↑⍴A)⍴A
  →0
EXPB:B←(RA,¯1↑⍴B)⍴B
∇

∇EXPAND;RA;NA;RB;NB
  ⍝ PROCEDURE TO TEST IF THE GLOBAL VARIABLES  A  AND  B  ARE
  ⍝ CONFORMABLE FOR SCALAR OPERATIONS AND IF SO, TO EXPAND
  ⍝ ONE OF THEM, IF NECESSARY, SO THAT THEY HAVE THE SAME
  ⍝ SHAPE.  IF THEY ARE NOT CONFORMABLE, ALL PROCESSING IS
  ⍝ STOPPED. IF EITHER  A  OR  B  HAS ONE ENTRY, THEY ARE
  ⍝ CONFOMRABLE.
  →(∧/1=(NA←×/RA←⍴A),NB←×/RB←⍴B)/EXP
  ⍝ OTHERWISE, THEY MUST HAVE THE SAME RANK.
  →((⍴RA)≠⍴RB)/RNKERR
  ⍝ AND THE SAME SHAPE.
  →(∧/RA≠RB)/LENERR
  →0
RNKERR:'PROCEDURE RANK ERROR'
  →
LENERR:'PROCEDURE LENGTH ERROR'
  →
  ⍝ SEE WHICH ARRAY MUST BE EXPANDED.
EXP:→((NA≠1)∧(NA=1)∧(NB=1)∧(⍴RA)>⍴RB)/EXB
  ⍝ EXPAND A
  →0,⍴A←RB⍴A
  ⍝ EXPAND B
EXB:B←RA⍴B
∇

∇B←TRAV A;R
  ⍝ TRANSPOSES AN ARRAY OF VECTORS.
  →(1≥⍴⍴A)/SMALL
  R←⍳⍴⍴A
  B←((⌽¯1↓R),¯1↑R)⍉A
  →0
SMALL:B←A
∇
