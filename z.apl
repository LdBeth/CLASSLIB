
∇C←A ZXDIFF B
  ⍝ COMPUTES THE DIFFERENCE OF TWO ARRAYS OF INTEGER
  ⍝ POLYNOMIALS.
  C←A ZXSUM-B
∇

∇C←A ZXSUM B;M;D
  ⍝ COMPUTES THE SUM OF TWO ARRAYS OF INTEGER POLYNOMIALS.
  →NOTEST/BEGIN
  DERR∧/(,A=⌊A),,B=⌊B
  EXPANDV
BEGIN:M←(⍴A)⌈⍴B
  C←(M×A)+M×B
  D←1⌈⌈/,+/∧\⌽C≠0
  C←((¯1↓⍴C),D)↑C
∇

∇P←ZFACTOR N;Q;R;⎕IO
  ⍝ FACTORS A POSITIVE INTEGER INTO A PRODUCT OF PRIMES.
  ⍝ THE RESULT IS CORRECT IF  N  IS LESS THAN 2.5E9.
  DERR∧/(N=⌊N),(1≤N),1=⍴N←,N
  P←⍳⎕IO←0
  Q←2 3 5,R←,(30×⍳77⌊⌈(N*0.5)÷30)∘.+7 11 13 17 19 23 29 31
LOOP:→(0=⍴Q←(0=Q|N)/Q)/NEXT
  P←P,1↑Q
  →LOOP,N←⌊N÷Q[0]
NEXT:→(N=1)/0
  →((¯1↑R)≥50000⌊N*0.5)/END
  →(R[0]≠7)/GEN
  R←(∧⌿0≠7 11∘.|R)/R
GEN:Q←R←R+2310
  →LOOP
END:P←P,N
∇

∇C←A ZGCD B;⎕IO;RHO;M;U;V;I;Q;T
  ⍝ C  IS RETURNED AS THE ENTRY-BY-ENTRY GCD OF THE INTEGER
  ⍝ ARRAYS  A  AND  B.
  ⍝ THE VARIABLES r AND s EXPRESS C AS(r×A)+s×B.
  ⎕IO←1
  →NOTEST/BEGIN
  DERR∧/(,A=⌊A),,B=⌊B
  ⍝ TEST FOR CONFORMABILITY.
  EXPAND
  ⍝ REPLACE  A  AND  B  BY THEIR RAVELS AND
  ⍝ APPLY THE EUCLIDEAN ALGORITHM.
BEGIN:M←×/RHO←⍴A
  U←(3,M)⍴(×A),(M⍴0),|A←,A
  V←(⍴U)⍴(M⍴0),(×B),|B←,B
  I←⍳M
LOOP:→(0=⍴I←(V[3;I]≠0)/I)/END
  T←U[;I]-V[;I]×(3,⍴I)⍴⌊U[3;I]÷V[3;I]
  U[;I]←V[;I]
  V[;I]←T
  →LOOP
END:C←RHO⍴U[3;]
  r←RHO⍴U[1;]
  s←RHO⍴U[2;]
  ∇
