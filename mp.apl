∇X←MPZFORM A
  ⍝ CONVERTS A VECTOR OF DIGITS TO THE BASE 1E6 TO
  ⍝ A VECTOR OF CHARACTERS.
  X←,((⍴A),¯6)↑⍕((⍴A),1)⍴⌊|A←,A
  X[(X=' ')/⍳⍴X]←'0'
  X←((¯1=×1↑A)⍴'¯'),((('0'≠¯1↓X)⍳1)-⎕IO)↓X
∇

∇A←MPZUNF X;⎕IO;M;SIGN
  ⍝ CONVERTS THE CHARACTER VECTOR OF A MULTIPLE PRECISION
  ⍝ INTEGER INTO A VECTOR OF DIGITS TO THE BASE 1E6.
  ⎕IO←0
  M←∨/'+¯'=1↑X←,X
  SIGN←1↑M↑X
  DERR∧/(X←M↓X)∊'0123456789'
  A←,⍎((7×M)⍴0 1 1 1 1 1 1)\(¯6×M←⌈(⍴X)÷6)↑X
  A←(¯1*SIGN='¯')×A
∇

∇C←A MPZSUM0 B;M
  ⍝ ADDS VECTORS OF DIGITS TO BASE 1E6.
  M←-(⍴A←,A)⌈⍴B←,B
  C←MPZNRMLZ(M↑A)+M↑B
∇

∇B←MPZNRMLZ A;SIGN;S
⍝ NORMALIZES A VECTOR OF DIGITS TO THE BASE 1E6.
  DERR∧/(1≥⍴⍴A),B=⌊B←,A
  SIGN←1
LOOP:→(0=⍴B←(((B≠0)⍳1)-⎕IO)↓B)/ZERO
  B←⌊B×S←×1↑B
SIGN←S×SIGN
  →(∧/(B<1000000),B≥0)/END
  B←(S,0)+0,B-1000000×S←⌊B÷1000000
  →LOOP
ZERO:→B←,0
END:B←SIGN×B
∇

∇Z←X MPZSUM Y
  ⍝ COMPUTES THE SUM OF TWO MULTIPLE PRECISION INTEGERS
  ⍝ IN CHARACTER FORM.
  Z←MPZFORM(MPZUNF X)MPZSUM0 MPZUNF Y
∇

∇Z←X MPZPROD Y
  ⍝ MULTIPLE PRECISION PRODUCT OF INTEGERS GIVEN
  ⍝ BY CHARACTER VECTORS OF DIGITS TO THE BASE 10.
  Z←MPZFORM(MPZUNF X)MPZPROD0 MPZUNF Y
∇

∇C←A MPZPROD0 B;⎕IO;U
  ⍝ COMPUTES THE PRODUCT OF TWO VECTORS OF DIGITS TO
  ⍝ THE BASE 1E6.
  ⎕IO←0
  U←(A←,A)∘.×,B
  C←MPZNRMLZ+⌿(-⍳⍴A)⌽U,(0 ¯1+2⍴⍴A)⍴0
∇

∇Z←X MPZDIFF Y
  ⍝ COMPUTES THE DIFFERENCE OF TWO MULTIPLE PRECISION
  ⍝ INTEGERS REPRESENTED AS CHARACTER VECTORS.
  Z←MPZFORM(MPZUNF X)MPZSUM0-MPZUNF Y
∇

∇Z←X MPZPOWER N
  ⍝ COMPUTES THE N-TH POWER OF THE MULTIPLE PRECISION
  ⍝ INTEGER WITH CHARACTER VECTOR  X.  N  IS AN
  ⍝ ORDINARY INTEGER.
  Z←MPZFORM(MPZUNF X)MPZPOWER0 N
∇

∇B←A MPZPOWER0 N
  ⍝ RAISES A VECTOR OF DIGITS TO BASE 1E6 TO
  ⍝ THE POWER N, WHICH IS AN ORDINARY INTEGER.
  DERR(N=⌊N)∧N≥0
  A←MPZNRMLZ A
  B←,1
LOOP:→(N=0)/0
  →(0=2|N)/EVEN
  B←B MPZPROD0 A
EVEN:A←A MPZPROD0 A
  N←⌊N÷2
  →LOOP
∇

∇Z←X MPZREM Y
  ⍝ COMPUTES THE REMAINDER WHEN THE MULTIPLE PRECISION
  ⍝ INTEGER  Y  IS DIVIDED BY THE MULTIPLE PRECISION
  ⍝ INTEGER  X.  THE INTEGER QUOTIENT IS SAVED IN THE
  ⍝ GLOBAL VARIABLE  q.  HERE  X  AND  Y  ARE
  ⍝ CHARACTER VECTORS OF DIGITS TO THE BASE 10.
  Z←MPZFORM(MPZUNF X)MPZREM0 MPZUNF Y
  q←MPZFORM q
∇

∇C←A MPZREM0 B;⎕IO;I;L;M;N;Q;R;SA;SC;T
  ⍝ COMPUTES MULTIPLE PRECISION REMAINDER USING VECTORS
  ⍝ OF DIGITS TO THE BASE 1E6.  THE QUOTIENT IS SAVED
  ⍝ IN THE GLOBAL VARIABLE  q.
  q←,⎕IO←0
  C←MPZNRMLZ B
  →(0=SA←×1↑A←MPZNRMLZ A)/0
  A←|A
LOOP:→(((⍴C)<⍴A)∧0>SC←×1↑C)/NEG
  →((⍴C)>⍴A)/DIVIDE
  →(0>SC)/DIVIDE
  →((⍴C)<⍴A)/0
  →((⍴A)=I←(A≠C)⍳1)/DIVIDE
  →(A[I]>C[I])/0
DIVIDE:N←⌊10⊥Q←(1000000⊥|(M←3⌊⍴C)↑C)÷1000000⊥(L←3⌊⍴A)↑A
  →(12>T←N+6×((⍴C)-M)-(⍴A)-L)/SMALL
  Q←⌊Q×10*T-N+6×R←¯2+⌊T÷6
  Q←(SA×SC×,(3⍴1000000)⊤Q),R⍴0
  →ADJUST
SMALL:Q←⌊SA×SC×Q×10*T-N
  Q←(×Q)×((1+⌊10⊥|Q)⍴1000000)⊤|Q
ADJUST:q←q MPZSUM0 Q
  C←C MPZSUM0(-SA)×Q MPZPROD0 A
  →LOOP
NEG:C←C MPZSUM0 A
  q←q MPZSUM0-SA
∇

∇B←MPZMAG A;N
  ⍝ COMPUTES THE ABSOLUTE VALUE OF AN INTEGER
  ⍝ GIVEN BY A CHARACTER VECTOR OF DIGITS TO
  ⍝ THE BASE 10.
  DERR(1≥⍴⍴A)∧∧/,A∊'0123456789+¯'
  →(~(1↑A←,A)∊'+¯')/NOSIGN
  A←1↓A
NOSIGN:N←+/∧\A='0'
  →(0<⍴B←N↓A)/0
  B←,'0'
∇

∇B←MPZMAG0 A
  ⍝ COMPUTES THE MAGNITUDE OF AN INTEGER GIVEN
  ⍝ BY A VECTOR OF DIGITS TO THE BASE 1E6.
  B←|MPZNRMLZ A
∇

∇B←MPZNEG A;⎕IO;I;C
  ⍝ COMPUTES THE NEGATIVE OF AN INTEGER GIVEN
  ⍝ BY A CHARACTER VECTOR OF DIGITS TO THE BASE 10.
  ⎕IO←0
  DERR(1≥⍴⍴A)∧' '=1↑0↑,A
  I←(C←1↑B←,A)∊'+¯'
  B←I↓B
  DERR∧/B∊'0123456789'
  B←(+/∧\'0'=¯1↓B)↓B
  →((C='¯')∧'0'=1↑B)/0
  B←'¯',B
∇

∇T←MPZSGN A
  ⍝ COMPUTES THE SIGNUM OF A MULTIPLE PRECISION INTEGER
  ⍝ GIVEN BY A CHARACTER VECTOR OF DIGITS TO THE BASE 10.
  DERR∧/(1≥⍴⍴A),,A∊'0123456789+¯'
  →('¯'≠1↑A←,A)/NONNEG
  T←¯1
  →0
NONNEG:→(∧/A∊'0+')/ZERO
  T←1
  →0
ZERO:T←0
∇

∇Z←X MPZGCD Y
  ⍝ COMPUTES THE GCD OF TWO INTEGERS GIVEN BY
  ⍝ CHARACTER VECTORS OF DIGITS TO THE BASE 10.
  Z←MPZFORM(MPZUNF X)MPZGCD0 MPZUNF Y
∇

∇C←A MPZGCD0 B;q
  ⍝ COMPUTES THE GCD OF TWO INTEGERS GIVEN
  ⍝ BY VECTORS OF DIGITS TO THE BASE 1E6.
  A←|MPZNRMLZ A
  C←|MPZNRMLZ B
LOOP:→(0=1↑A)/0
  B←A MPZREM0 C
  C←A
  A←B
  →LOOP
∇

∇D←MPZDET A;⎕IO;E;N;n;F;G;P;I;q;N2
  ⍝ MULTIPLE PRECISION DETERMINANT OF A SINGLE PRECISION
  ⍝ INTEGER MATRIX A LA CABAY AND LAM.
  DERR∧/(2=⍴⍴A),(=/⍴A),,A=⌊A
  ⎕IO←1
  E←(⍟2)++/⍟(+/A×A)*0.5
  DERR(⍴BIGPRIMES)≥N←⌈E÷⍟BIGPRIMES[1]
  N2←⌊0.5×n←BIGPRIMES[1]
  D←,(n|N2+ZNDET A)-N2
  P←,1
  I←1
LOOP:→(N<I←I+1)/END
  N2←⌊0.5×n←BIGPRIMES[I]
  F←ZNDET A
  G←(n|N2+BIGINV[I]×F-1000000⊥(,n)MPZREM0 D)-N2
  P←P MPZPROD0,BIGPRIMES[I-1]
  D←D MPZSUM0 P MPZPROD0,G
  →LOOP
END:D←MPZFORM D
∇
