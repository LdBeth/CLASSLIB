∇X←N GPCYCIN C;Y;I;D;U
  ⍝ CONSTRUCTS THE VECTOR FORM OF THE PERMUTATION OF  ⍳N
  ⍝ GIVEN AS A PRODUCT OF CYCLES IN THE CHARACTER VECTOR  C.
  ⍝ THE CYCLES DO NOT NEED TO BE DISJOINT.  ORIGIN DEPENDENT.
  DERR(1=⍴N)∧N=⌊N←,N
  X←⍳N
  C←(C≠' ')/C←,C
  ⍝ GET THE NEXT CYCLE.
LOOP:→(0=⍴C)/0
  D←(I←(C⍳')')+1-⎕IO)↑C
  C←I↓C
  DERR('('=1↑D)∧')'=¯1↑D
  D←¯1↓1↓D
  D[(D=',')/⍳⍴D]←' '
  DERR∧/D∊'0123456789'
  DERR(⍴U)=⍴SSORT U←,¯D
  DERR∧/U<N+⎕IO
  Y←⍳N
  Y[U]←1⌽U
  X←Y[X]
  →LOOP
∇

∇C←GPCYCOUT X;T;I;J
  ⍝ CONSTRUCTS THE CYCLE FORM OF THE PERMUTATION  X.
  DERR 1=⍴⍴X
  DERR∧/X[⍋X]=⍳⍴X
  C←''
  T←(⍴X)⍴0
  ⍝ FIND THE START OF THE NEXT CYCLE.
LOOP1:→((⎕IO+⍴X)≤I←T⍳0)/0
  T[I]←1
  C←C,'(',⍕J←I
LOOP2:→(I=J←X[J])/CLOSE
  C←C,',',⍕J
  T[J]←1
  →LOOP2
CLOSE:C←C,')'
  →LOOP1
∇

∇T←GPSYMG N;V
  ⍝ LISTS THE ELEMENTS OF THE SYMMETRIC GROUP ON  ⍳N.
  DERR∧/(0≤N←''⍴N),(N=⌊N),1=⍴N←,N
  →(N>0)/GENERAL
  T←1 0⍴0
  →0
GENERAL:T←((!N),N-1)⍴GPSYMG N-1
  V←,⍉((!N-1),N)⍴(⍳N)-⎕IO
  T←V⌽((-V)⌽T),N+⎕IO-1
∇

∇T←GPTEST A;M;N;Z
  ⍝ CHECKS THAT  A  IS AN ARRAY OF PERMUTATIONS.
  →(~T←∧/(1≤⍴⍴A),,A=⌊A)/0
  →(~T←(∧/,A≥⎕IO)∧∧/A<⎕IO+N←¯1↑⍴A)/0
  Z←(×/⍴A)⍴0
  Z[(,A)+,⍉(N,M)⍴N×(⍳M←×/¯1↓⍴A)-⎕IO]←1
  T←∧/Z
∇

∇H←GPSGP X;N;HP;V;VP
  ⍝ COMPUTES THE PERMUTATION GROUP GENERATED BY THE
  ⍝ ROWS OF THE MATRIX  X.  VALID FOR DEGREES AT MOST 12.
  ⍝ WORKSPACE FULL ERRORS ARE LIKELY FOR DEGREES OVER 7.
  DERR(GPTEST X)∧(2=⍴⍴X)∧12≥¯1↑⍴X
  N←¯1↑⍴X
  H←(N⍴N+1)⊤HP←SSORT(N+1)⊥(⍳N),V←X←⍉X
LOOP:HP←HP,VP←SSORT(~VP∊HP)/VP←,(N+1)⊥X[V;]
  H←H,V←(N⍴N+1)⊤VP
  →(0≠⍴VP)/LOOP
  H←⍉H
∇

∇B←GPSGN A;C;D;N
  ⍝ COMPUTES THE SIGNS OF THE PERMUTATIONS IN  A.
  →NOTEST/BEGIN
  DERR GPTEST A
BEGIN:D←((⍳¯2+⍴⍴C),⎕IO+(⍴⍴C)-1 2)⍉C←A∘.×(N←¯1↑⍴A)⍴1
  B←¯1*+/+/(C>D)∧(⍴C)⍴(⍳N)∘.<⍳N
∇

∇C←A GPORBIT I;V
  ⍝ COMPUTES A LIST  C  AND THE CHARACTERISTIC VECTOR  x
  ⍝ OF THE ORBIT CONTAINING  I  OF THE PERMUATION GROUP
  ⍝ GENERATED BY THE ROWS OF THE MATRIX  A.
  DERR(1=⍴I)∧(2=⍴⍴A)∧(∧/I=⌊I←,I)∧GPTEST A
  x←(¯1↑⍴A)⍴0
  x[C←V←I]←1
LOOP:C←C,V←SSORT(~x[V])/V←,A[;V]
  x[V]←1
  →(0≠⍴V)/LOOP
  C←SSORT C
∇

∇B←GPALLORB A;N;x;I;NOTEST;C
  ⍝ COMPUTES A SUMMARY OF THE ORBITS OF THE PERMUTATION
  ⍝ GROUP GENERATED BY THE ROWS OF THE MATRIX  A.
  ⍝ THE FIRST ROW OF  B  GIVES THE LENGTHS OF THE ORBITS.
  ⍝ THE SECOND ROW OF  B  GIVES REPRESENTATIVES.
  ⍝ q[I]  IS THE FIRST POINT IN THE ORBIT CONTAINING  I.
  DERR(GPTEST A)∧2=⍴⍴A
  q←(N←¯1↑⍴A)⍴¯1
  B←2 0⍴0
  NOTEST←1
LOOP:→((N+⎕IO)≤I←q⍳¯1)/0
  q[C←A GPORBIT I]←I
  B←B,I,⍴C
  →LOOP
∇
